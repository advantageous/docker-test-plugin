buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.4"
    }
}

group 'io.advantageous.gradle'
version '0.1.7'

apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: "com.gradle.plugin-publish"

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

project.task("wrapper", type: Wrapper) {
    gradleVersion = '2.11'
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'junit:junit:4.11'
    compile 'io.advantageous.boon:boon-util:0.5.7'
}

pluginBundle {
    website = 'https://github.com/advantageous/docker-test-plugin'
    vcsUrl = 'https://github.com/advantageous/docker-test-plugin.git'
    description = 'Spin up docker containers for integration tests.'
    tags = ['testing', 'docker', 'container', 'integration testing', 'continuous integration']

    plugins {
        dockerTestPlugin {
            id = 'io.advantageous.docker-test'
            displayName = 'Docker Integration Tests'
        }
    }
}
signing {
    required false
    sign configurations.archives
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                try {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                } catch (MissingPropertyException ignore) {
                }
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                try {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                } catch (MissingPropertyException ignore) {
                }
            }

            pom.project {
                packaging 'jar'
                url 'https://github.com/advantageous/docker-test-plugin'
                description 'Spin up docker containers for integration tests.'
                name project.name

                scm {
                    url 'scm:git@github.com:advantageous/docker-test-plugin.git'
                    connection 'scm:git@github.com:advantageous/docker-test-plugin.git'
                    developerConnection 'scm:git@github.com:advantageous/docker-test-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'sailorgeoffrey'
                        name 'Geoffrey Chandler'
                    }
                }
            }
        }
    }
}
